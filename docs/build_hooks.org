There are some operations that we need to perform before compilation.
Shadow-cljs offers a tool called Build Hooks in which you can
do certain tasks at different points in the lifecycle of the build and
of the program itself.


#+begin_src clojurescript :tangle ../src/build_hooks.clj
(ns build-hooks
  (:require [clojure.java.io :as io]
            [clojure.string :as str]
            ))
#+end_src


** Prebuild file preperation


This allows us to ensure the wasm winds up in the deploy files for Vite to serve out.
Since we're mixing ShadowCLJS and Vite we often just want ShadowCLJS to only
worry about the code itself.
#+begin_src clojurescript :tangle ../src/build_hooks.clj
(defn copy-wasm
  {:shadow.build/stage :flush}
  [build-state & args]
  (let [source (io/file "src/generated-compat/wasm-bindgen/index_bg.wasm")
        target (io/file "resources/public/generated-compat/wasm-bindgen/index_bg.wasm")]
    (when (.exists source)
      (io/make-parents target)
      (io/copy source target)
      (println "--- WASM copied to public/js ---")))
  build-state)
#+end_src


However, for dynamic resource allocation... Vite doesn't have a great way to do this
So instead we want to do this BEFORE ShadowCLJS compiles even so that we can be
sure our translation files are present in time.
#+begin_src clojurescript :tangle ../src/build_hooks.clj
(defn generate-lang-map
{:shadow.build/stage :configure}
  [build-state & args]
  (let [source-dir (io/file "node_modules/@element-hq/web-shared-components/src/i18n/strings")
        target-dir (io/file "resources/public/i18n")
        files (filter #(.endsWith (.getName %) ".json") (file-seq source-dir))]
    (println "Syncing I18n JSONs to resources/public/i18n...")
    (.mkdirs target-dir)
    (let [mapping (into {} (for [f files]
                             (let [name (.getName f)
                                   lang (second (re-find #"^([^.]+)" name))]
                               (io/copy f (io/file target-dir name))
                               [lang (str "/i18n/" name)])))
          content (str "(ns client.i18n-map)\n\n"
                       "(def urls " (pr-str mapping) ")")]
      (io/make-parents "src/client/i18n_map.cljs")
      (spit "src/client/i18n_map.cljs" content))
    build-state))
#+end_src

